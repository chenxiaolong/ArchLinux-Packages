# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# Run tests? They take a bit of time.
run_tests=false

pkgname=certmonger
pkgver=0.74
pkgrel=1
pkgdesc="Certificate status monitor and PKI enrollment client"
arch=('i686' 'x86_64')
url="http://certmonger.fedorahosted.org"
license=('GPL')
depends=('curl' 'dbus' 'libxml2' 'nspr' 'nss' 'openssl' 'systemd' 'talloc' 'tevent' 'xmlrpc-c')
checkdepends=('diffutils' 'dos2unix' 'expect')
backup=('etc/certmonger/certmonger.conf'
        'etc/tmpfiles.d/certmonger.conf')
install=${pkgname}.install
source=("http://fedorahosted.org/released/certmonger/certmonger-${pkgver}.tar.gz"
        '0001_Fix_includes.patch')
sha512sums=('0a059d4d7c6576e27b7469b2b95e876e067cb7320772dbbfc8f814cdd12c268efb5987fe56f9b2f53d6e4e53b1bcdfabbe26931ffce4378396f3cdd299357755'
            '0139a4584fcf4e93b5092d9d521e1a701ffbe6974ad7651b1bdb852a5223532f88fc178da5136181ba22a8698e6961541d0bf5a83f2711bd78fc2a2ebf5f784b')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i "${srcdir}/0001_Fix_includes.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/${pkgname} \
    --localstatedir=/var \
    --enable-systemd \
    --enable-tmpfiles \
    --with-tmpdir=/run/certmonger

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  if [ "x${run_tests}" == "xtrue" ]; then
    make check
  fi
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
}
