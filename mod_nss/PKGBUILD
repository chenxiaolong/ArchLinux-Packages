# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=mod_nss
pkgver=1.0.8
pkgrel=1
pkgdesc="SSL/TLS module for the Apache HTTP server"
arch=('i686' 'x86_64')
url="http://directory.fedoraproject.org/wiki/Mod_nss"
license=('Apache')
depends=('apache<2.4' 'nspr' 'nss')
backup=('etc/httpd/conf/extra/nss.conf'
        'etc/httpd/alias/secmod.db'
        'etc/httpd/alias/cert8.db'
        'etc/httpd/alias/key3.db'
        'etc/httpd/alias/install.log')
install=mod_nss.install
# Patches are from Fedora
source=("http://directory.fedoraproject.org/sources/mod_nss-${pkgver}.tar.gz"
        'mod_nss-wouldblock.patch'
        'mod_nss-negotiate.patch'
        'mod_nss-reverseproxy.patch'
        'mod_nss-pcachesignal.h'
        'mod_nss-reseterror.patch'
        'mod_nss-lockpcache.patch'
        'mod_nss-httpd24.patch'
        'mod_nss-overlapping_memcpy.patch')
sha512sums=('4081435bb6cf256f6deb63d109a44b4f4417fe243fa1fdfc32a2b871e62361334c5c22da6237f45ec02dbb6e5533fb60e2fe2cbed31b138ffccc19c5c5a387f8'
            '4a7605807a6d47d45b521e157356359650e80b53762692d79943fe67299849da7582f9389a733ab9f95d4c360098d3019fcd62867e0dacfecb579e4ba38a06ef'
            '15b99b622e2fd40ebc8e3b928007eb3e228c4455007aa55ac105e12a53bbe92bfef6168b286d882e9d95d0f6241fc762aadee55524643ea373c9bf920bd9c351'
            '201957a812306c2fc7fb956ef0f2665692e9fabbbf75fec815b3993a8d56987a0cefa239c34d6c2ad367266b90404a8179c1f28e2f91c37ffadc91eae205b6b5'
            '2ef64eb253fa1ffd17457d9bed6a7804183fc3d7c6fdab6bd588ca5473733ff4dc89be261dd0e9290babf5448dd3fffe189ac981d368ebdd7251d3e36ff65d1d'
            '3add38f1fc84c9e8a2ca297a1647fb371b9b8f88933b221074defd0bdce1d56639981dfc1783f96f418bf0528d76d233b120159ca7c2a8919a82d85078026494'
            'a4d7bcd5c42edf399b73c936f4654e2b5212578b4c1eef6797cc17783f07c2296368da53e675240f24d091c439454600578be1dd003edfc33d1601eefcc6a6fa'
            '334622f1806ba53e6ca76301fa318fe644c858c99f4e5b8c4180cda351632c32f84cf03a6993cd27f732bfdafc82c5a6e913dfddcdf16f388f2fd3fa304ab5aa'
            'c8ea0a0fa666f7a27f3895540cf8bf0c242fc5e2f6fb58946b31e21f24872ab4bfd15a2b06807475ca6307964fbe9c45293ea9f7a23b7a7648c183429195f84f')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/mod_nss-wouldblock.patch"
  patch -p1 -i "${srcdir}/mod_nss-negotiate.patch"
  patch -p1 -i "${srcdir}/mod_nss-reverseproxy.patch"
  patch -p1 -i "${srcdir}/mod_nss-pcachesignal.h"
  patch -p1 -i "${srcdir}/mod_nss-reseterror.patch"
  patch -p1 -i "${srcdir}/mod_nss-lockpcache.patch"
  #patch -p1 -i "${srcdir}/mod_nss-httpd24.patch"
  patch -p1 -i "${srcdir}/mod_nss-overlapping_memcpy.patch"

  # Update timestamps to avoid regeneration
  touch nss_expr_*.[chyl]
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export APXS=/usr/bin/apxs

  ./configure --prefix=/usr --with-apr-config
  make all
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -dm755 "${pkgdir}/etc/httpd/conf/extra/"
  install -dm755 "${pkgdir}/etc/httpd/alias/"
  install -dm755 "${pkgdir}/usr/lib/httpd/modules/"
  install -dm755 "${pkgdir}/usr/bin/"

  install -m644 nss.conf "${pkgdir}/etc/httpd/conf/extra/"
  install -m755 .libs/libmodnss.so "${pkgdir}/usr/lib/httpd/modules/"
  install -m755 nss_pcache "${pkgdir}/usr/bin/"
  install -m755 gencert "${pkgdir}/usr/bin/"

  perl -pi -e "s:/usr/lib:/usr/bin:" "${pkgdir}/usr/bin/gencert"

  ln -s ../../../usr/lib/libnssckbi.so "${pkgdir}/etc/httpd/alias/"
  touch "${pkgdir}/etc/httpd/alias/secmod.db"
  touch "${pkgdir}/etc/httpd/alias/cert8.db"
  touch "${pkgdir}/etc/httpd/alias/key3.db"
  touch "${pkgdir}/etc/httpd/alias/install.log"
}
