# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Jakub Schmidtke <sjakub-at-gmail-dot-com>
# Contributor: mosra <mosra@centrum.cz>

pkgname=kdevplatform-git
pkgver=v1.7.0.r621.g509358a
pkgrel=1
epoch=1
pkgdesc="A C/C++ development platform for KDE - Git build"
arch=(i686 x86_64)
url="http://www.kdevelop.org/"
license=(GPL)
groups=(kde)
depends=(karchive kconfig kguiaddons ki18n kitemmodels kitemviews kjobwidgets
         kcmutils kio knewstuff knotifyconfig kparts kservice sonnet
         ktexteditor threadweaver kwindowsystem kdeclarative kdelibs4support
         kdoctools libkomparediff2-git qt5-quick1 boost-libs)
makedepends=(cmake boost git)
optdepends=('kdesdk-kompare: difference checking')
provides=(kdevplatform)
conflicts=(kdevplatform)
install='kdevplatform.install'
source=('git+git://anongit.kde.org/kdevplatform')
sha512sums=('SKIP')

pkgver() {
  cd kdevplatform
  git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  mkdir -p build
  cd build

  cmake ../kdevplatform \
    -DLibKompareDiff2_DIR=/opt/kf5/lib/cmake/LibKompareDiff2 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
}
