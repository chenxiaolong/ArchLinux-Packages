# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# Based on commit 743ef0138f58293394ffbb763660acdf5a18bbf8 from the fedpkg
# master branch for freeipa.

# NOTE: ntp, autofs, and ntp support is currently broken because the formats of
# the /etc/conf.d/ configuration files in Arch Linux differ from the formats of
# Fedora's /etc/sysconfig/ configuration files.

# Active directory trusts will not (probably never) work in Arch. It requires a
# heavily patched version of Samba 4, which uses MIT Kerberos instead of
# Heimdal. (Fedora went through 174 releases to get this working O_O)

pkgname=freeipa
pkgver=4.2.0
pkgrel=1
pkgdesc="The Identity, Policy, and Audit system"
arch=(i686 x86_64)
url="http://www.freeipa.org/"
license=(GPL)

depends=(autofs
         bind
         certmonger
         curl
         cyrus-sasl-gssapi
         gnupg
         iproute2
         #keyutils
         krb5
         nfs-utils
         nfsidmap
         nss
         ntp
         oddjob
         openldap
         pam-krb5
         pypy-backports.ssl_match_hostname
         python2-dateutil
         python2-dnspython
         python2-kerberos
         python2-krbv
         python2-ldap
         python2-lxml
         python2-netaddr
         python2-nss
         python2-pyasn1
         python2-pyopenssl
         python2-qrcode
         python-pyusb
         python2-memcached
         python2-dbus
         sssd
         wget
         yp-tools
         xmlrpc-c)

# authconfig is Fedora specific
#depends+=(authconfig)

makedepends=(python2-distribute)

# Dependencies for "make check"
checkdepends=(check python2-nose)

options=(!libtool)
backup=(etc/ipa/default.conf
        etc/ipa/ca.crt)
install=freeipa.install
source=("http://www.freeipa.org/downloads/src/freeipa-${pkgver}.tar.gz"
        sss-auth-setup.py
        0001-Add-Arch-Linux-platform.patch
        0004-Fix-import-for-match_hostname.patch)
sha512sums=('ef31e4f443a66e9d13068992d65b24aa68c54e6cdabbab3e7230b3fdf6daa0e507fb0c91c6c0c5ae8c2e8a30d1e76511e3d6b8e15548a908be1a8c8bb6c8aa38'
            '5f101692e311205b3706642c6f329459646aaa693683ab2d4847bd8a7f464ef99ec617b0422df8e25ec2a0dc3a68cd9bf54db4bb3013b84844df15160716adc8'
            'a1f5977a54c5cee39b056ff1115f4335f5cbee42a08c309269b3231e0c2d44e527854c9e88aaf36980e958e8dbb04c46b068b6e0d70f9c1ef02aa20c0136bc39'
            '10afa71e190e6090ed85d7944a5d6bb99eb7eb8f6a07be37413550d08653967424caa8aea0a75e829d52cf947dba330e7dc8d003c0396e6e5580718dcc1bfa34')

build() {
    cd "${pkgname}-${pkgver}"

    patch -p1 -i ../0001-Add-Arch-Linux-platform.patch
    patch -p1 -i ../0004-Fix-import-for-match_hostname.patch

    export PYTHON=python2
    export SUPPORTED_PLATFORM=archlinux

    # Force regeneration of platform support
    rm -f ipapython/version.py
    rm -f ipaplatform/services.py
    rm -f ipaplatform/tasks.py
    rm -f ipaplatform/paths.py
    make version-update

    pushd ipa-client
    ../autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --with-openldap
    popd

    make IPA_VERSION_IS_GIT_SNAPSHOT=no client
}

check() {
    cd "${pkgname}-${pkgver}"
    # Tests require FreeIPA to be installed and set up
    #make test
}

package() {
    cd "${pkgname}-${pkgver}"

    # Force regeneration of platform support
    rm -f ipapython/version.py
    rm -f ipaplatform/services.py
    rm -f ipaplatform/tasks.py
    rm -f ipaplatform/paths.py
    make version-update

    make client-install DESTDIR="${pkgdir}"

    install -dm755 "${pkgdir}/var/lib/ipa-client/sysrestore/"

    # /etc/ipa/ is needed for ipa-client-install
    install -dm755 "${pkgdir}/etc/ipa/"

    # Fix filenames
    pushd "${pkgdir}/usr/lib/python2.7/site-packages/"
    mv ipapython-${pkgver}*-py2.7.egg-info ipapython-${pkgver}-py2.7.egg-info
    popd

    find "${pkgdir}/" \( -name '*.pyc' -o -name '*.pyo' \) -delete

    # Not packaging the tests for now
    find "${pkgdir}/" -type f | grep '\.py' | grep ipatests | xargs rm -f

    install -Dm755 /home/vagrant/ArchLinux-Packages/freeipa/sss-auth-setup.py \
        "${pkgdir}/usr/bin/sss-auth-setup"
}
