# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# Based on commit 743ef0138f58293394ffbb763660acdf5a18bbf8 from the fedpkg
# master branch for freeipa.

# NOTE: ntp, autofs, and ntp support is currently broken because the formats of
# the /etc/conf.d/ configuration files in Arch Linux differ from the formats of
# Fedora's /etc/sysconfig/ configuration files.

# Active directory trusts will not (probably never) work in Arch. It requires a
# heavily patched version of Samba 4, which uses MIT Kerberos instead of
# Heimdal. (Fedora went through 174 releases to get this working O_O)

pkgname=freeipa
pkgver=4.1.2
pkgrel=1
pkgdesc="The Identity, Policy, and Audit system"
arch=(i686 x86_64)
url="http://www.freeipa.org/"
license=(GPL)

depends=(autofs
         bind
         certmonger
         curl
         cyrus-sasl-gssapi
         gnupg
         iproute2
         #keyutils
         krb5
         nfs-utils
         nfsidmap
         nss
         ntp
         oddjob
         openldap
         pam-krb5
         pypy-backports.ssl_match_hostname
         python2-dateutil
         python2-dnspython
         python2-kerberos
         python2-krbv
         python2-ldap
         python2-lxml
         python2-netaddr
         python2-nss
         python2-pyasn1
         python2-pyopenssl
         python2-qrcode
         sssd
         wget
         xmlrpc-c)

# authconfig is Fedora specific
#depends+=(authconfig)

makedepends=(python2-distribute)

# Dependencies for "make check"
checkdepends=(check python2-nose)

options=(!libtool)
backup=(etc/ipa/default.conf
        etc/ipa/ca.crt)
install=freeipa.install
source=("http://www.freeipa.org/downloads/src/freeipa-${pkgver}.tar.gz"
        sss-auth-setup.py
        0001-Add-Arch-Linux-platform.patch
        0002-Use-Python-2.patch
        0003-Fix-nosetests-path.patch
        0004-Fix-import-for-match_hostname.patch)
sha512sums=('eb22201dfa84e9836ea34327d7d1ae3b407e77eff6dc8acf9d4c619b26a48b59d0fdb486b97c32010a966e35543b8a98d3dea7911dd567146c207c8b3a78240d'
            '5f101692e311205b3706642c6f329459646aaa693683ab2d4847bd8a7f464ef99ec617b0422df8e25ec2a0dc3a68cd9bf54db4bb3013b84844df15160716adc8'
            '4f515303140307ea9f8f6b48401284316390645e9f0f8eb8589a56664cc6a1b8a1c195f58c6a34bcff1241f2ef5588083b68a25b26e2e5c7c5d43f9ffcc4181c'
            '9387ea7ae81d042e554530c6dc2d050c13bb0f4f0e04d98a3bae545a0bf630b61d3491415fe9a0e203f44416eba693dda0f8d9b40c714b388856e5c9c105ded8'
            '6bd1c88ea03ba1a15caba5b9f0e2ca294fc620798b25840948976f6f7f0ad1574de54bcd3cd5b0052d32a9741acadc588ab4eb0301ece0deac9d10989ce26ec6'
            '10afa71e190e6090ed85d7944a5d6bb99eb7eb8f6a07be37413550d08653967424caa8aea0a75e829d52cf947dba330e7dc8d003c0396e6e5580718dcc1bfa34')

build() {
    cd "${pkgname}-${pkgver}"

    patch -p1 -i ../0001-Add-Arch-Linux-platform.patch
    patch -p1 -i ../0002-Use-Python-2.patch
    patch -p1 -i ../0003-Fix-nosetests-path.patch
    patch -p1 -i ../0004-Fix-import-for-match_hostname.patch

    export PYTHON=python2
    export SUPPORTED_PLATFORM=archlinux

    # Force regeneration of platform support
    rm -f ipapython/version.py
    rm -f ipaplatform/services.py
    rm -f ipaplatform/tasks.py
    rm -f ipaplatform/paths.py
    make version-update

    pushd ipa-client
    ../autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --with-openldap
    popd

    make IPA_VERSION_IS_GIT_SNAPSHOT=no client
}

check() {
    cd "${pkgname}-${pkgver}"
    # Tests require FreeIPA to be installed and set up
    #make test
}

package() {
    cd "${pkgname}-${pkgver}"

    # Force regeneration of platform support
    rm -f ipapython/version.py
    rm -f ipaplatform/services.py
    rm -f ipaplatform/tasks.py
    rm -f ipaplatform/paths.py
    make version-update

    make client-install DESTDIR="${pkgdir}"

    install -dm755 "${pkgdir}/var/lib/ipa-client/sysrestore/"

    # /etc/ipa/ is needed for ipa-client-install
    install -dm755 "${pkgdir}/etc/ipa/"

    # Fix filenames
    pushd "${pkgdir}/usr/lib/python2.7/site-packages/"
    mv ipapython-${pkgver}*-py2.7.egg-info ipapython-${pkgver}-py2.7.egg-info
    popd

    find "${pkgdir}/" \( -name '*.pyc' -o -name '*.pyo' \) -delete

    # Not packaging the tests for now
    find "${pkgdir}/" -type f | grep '\.py' | grep ipatests | xargs rm -f

    install -Dm755 sss-auth-setup.py \
        "${pkgdir}/usr/bin/sss-auth-setup"
}
